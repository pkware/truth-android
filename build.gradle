subprojects {
    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.2.2'
        }
    }

    repositories {
        jcenter()
    }

    group = GROUP
    version = VERSION_NAME
}

ext {
    minSdkVersion = 15
    compileSdkVersion = 25
    buildToolsVersion = '25.0.0'
    supportLibVersion = '25.0.1'

    truth = 'com.google.truth:truth:0.30'
    supportAnnotations = "com.android.support:support-annotations:${supportLibVersion}"
    supportV4 = "com.android.support:support-v4:${supportLibVersion}"
    supportAppCompat = "com.android.support:appcompat-v7:${supportLibVersion}"
    supportCardView = "com.android.support:cardview-v7:${supportLibVersion}"
    supportDesign = "com.android.support:design:${supportLibVersion}"
    supportGridLayout = "com.android.support:gridlayout-v7:${supportLibVersion}"
    supportMediaRouter = "com.android.support:mediarouter-v7:${supportLibVersion}"
    supportPalette = "com.android.support:palette-v7:${supportLibVersion}"
    supportRecyclerView = "com.android.support:recyclerview-v7:${supportLibVersion}"

    playServices = 'com.google.android.gms:play-services:7.5.0'
}

subprojects {
    afterEvaluate {
        android.libraryVariants.all { variant ->
            def task = project.tasks.create "jar${variant.name.capitalize()}", Jar
            task.description = "Create jar artifact for ${variant.name}"
            task.dependsOn variant.javaCompile
            task.from variant.javaCompile.destinationDir
            task.destinationDir = project.file("${project.buildDir}/outputs/jar")
            task.archiveName = "${project.name}-${variant.baseName}.jar"
            artifacts.add('archives', task);
        }
    }
}